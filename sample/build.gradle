apply plugin: 'com.android.application'

android {
    compileSdkVersion project.androidTargetSdk
    buildToolsVersion project.androidBuildTools

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion project.androidTargetSdk
        versionCode 360
        versionName "3.6.0"
    }

    def validConfig
    def keystoreFile
    def keystorePassword
    def keystoreAlias

    try {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        keystoreFile = properties.getProperty('keystore.file')
        keystorePassword = properties.getProperty('keystore.password')
        keystoreAlias = properties.getProperty('keystore.alias')
        validConfig = keystoreFile != null && keystorePassword != null && keystoreAlias != null;
    } catch (error) {
        validConfig = false;
    }

    if (validConfig) {
        System.out.println("Release signing configured with " + keystoreFile)
        signingConfigs {
            release {
                storeFile project.rootProject.file(keystoreFile)
                storePassword keystorePassword
                keyAlias keystoreAlias
                keyPassword keystorePassword
            }
        }
    } else {
        System.out.println("Specify keystore.file, keystore.alias and keystore.password in local.properties to enable release signing.")
    }

    buildTypes {
        release {
            if (validConfig) {
                signingConfig signingConfigs.release
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}


dependencies {
    // If you use this from an external project, use the following instead:
    //   compile 'com.journeyapps:zxing-android-embedded:<version>'
    implementation project(':zxing-android-embedded')

    // leakcanary is for development purposes only
    // https://github.com/square/leakcanary
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-v13:28.0.0'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    // AboutLibraries
    implementation "com.mikepenz:aboutlibraries:6.2.3"

}
