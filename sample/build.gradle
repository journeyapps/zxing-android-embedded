apply plugin: 'com.android.application'

android {
    compileSdkVersion project.androidTargetSdk
    buildToolsVersion project.androidBuildTools

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion project.androidTargetSdk
        versionCode 330
        versionName "3.3.0"
    }

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def keystoreFile = properties.getProperty('keystore.file')
    def keystorePassword = properties.getProperty('keystore.password')
    def keystoreAlias = properties.getProperty('keystore.alias')
    def validConfig = keystoreFile != null && keystorePassword != null && keystoreAlias != null;

    if(validConfig) {
        System.out.println("Release signing configured with " + keystoreFile)
        signingConfigs {
            release {
                storeFile project.rootProject.file(keystoreFile)
                storePassword keystorePassword
                keyAlias keystoreAlias
                keyPassword keystorePassword
            }
        }
    } else {
        System.out.println("Specify keystore.file, keystore.alias and keystore.password in local.properties to enable release signing.")
    }

    buildTypes {
        release {
            if(validConfig) {
                signingConfig signingConfigs.release
            }

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

dependencies {
    // If you use this from an external project, use the following instead:
    //   compile 'com.journeyapps:zxing-android-embedded:<version>@aar'
    //   compile 'com.google.zxing:core:3.2.0'
    compile(project(':zxing-android-embedded')) { transitive = true }
    compile 'com.android.support:appcompat-v7:23.1.0'


    // For development purposes only
    // https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
